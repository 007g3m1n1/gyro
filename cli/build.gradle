plugins {
    id 'org.springframework.boot' version '2.1.0.RELEASE'
}

dependencies {
    compile project(':gyro-core')
}

if (System.getenv("TRAVIS_BRANCH") == null) {
    defaultTasks 'dist'
} else {
    check.dependsOn 'bundleDistribution', 'publish'
    defaultTasks 'bundleDistribution', 'publish'
}

publish.dependsOn assemble
publish.dependsOn 'bundleDistribution'
publishToMavenLocal.dependsOn 'bundleDistribution'

def OS_NAME = System.getenv("TRAVIS_OS_NAME") ?: "unix"

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'gyro'
            artifactId = "gyro-${OS_NAME}"

            artifact("dist/gyro-cli-${OS_NAME}-${version}.zip") {
                extension 'zip'
            }

            artifact("dist/gyro")
        }
    }
}

task distDir {
    doLast {
        mkdir "dist"
    }
}

task dist(type: Copy) {
    from jar.archivePath
    rename '.+', 'gyro'
    into 'dist'

    dependsOn 'distDir'
}

task bundleDistribution(type: Exec) {
    environment OS_NAME: "${OS_NAME}"
    environment VERSION: "${version}"
    commandLine 'sh', '-c', 'scripts/build-gyro-dist.sh'

    dependsOn 'dist'
}

bootJar {
    classifier = ''
    mainClassName = 'gyro.cli.Gyro'
    launchScript {
        script = file('scripts/stub.sh')
    }
}
