
plugins {
    id 'maven-publish'
    id 'org.springframework.boot' version '2.1.0.RELEASE'
}
apply plugin: 'java'
apply plugin: 'io.spring.dependency-management'

sourceCompatibility = 1.8

if (System.getenv("TRAVIS_BRANCH") == null) {
    defaultTasks 'dist'
} else {
    check.dependsOn 'bundleDistribution', 'publish'
    defaultTasks 'bundleDistribution', 'publish'
}

publish.dependsOn 'bundleDistribution'
publishToMavenLocal.dependsOn 'bundleDistribution'

dependencies {
    compile "gyro:core:${version}"
}

def OS_NAME = System.getenv("TRAVIS_OS_NAME") ?: "unix"

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'gyro'
            artifactId = "gyro-${OS_NAME}"

            artifact("dist/gyro-cli-${OS_NAME}-${version}.zip") {
                extension 'zip'
            }

            artifact("dist/gyro-cli")
        }
    }

    repositories {
        maven {
            credentials {
                username System.getenv("GRADLE_DEPLOY_USER")
                password System.getenv("GRADLE_DEPLOY_PASSWORD")
            }

            if (project.version.endsWith('-SNAPSHOT')) {
                url "https://artifactory.psdops.com/gyro-snapshots"
            } else {
                url "https://artifactory.psdops.com/gyro-releases"
            }
        }
    }
}

task distDir {
    doLast {
        mkdir "dist"
    }
}

task dist(type: Copy) {
    from jar.archivePath
    rename '.+', 'gyro'
    into 'dist'

    dependsOn 'distDir'
}

task bundleDistribution(type: Exec) {
    environment OS_NAME: "${OS_NAME}"
    environment VERSION: "${version}"
    commandLine 'sh', '-c', 'scripts/build-gyro-dist.sh'

    dependsOn 'dist'
}

bootJar {
    classifier = ''
    mainClassName = 'gyro.cli.Gyro'
    launchScript {
        script = file('scripts/stub.sh')
    }
}
