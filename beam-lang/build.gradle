
plugins {
    id 'maven-publish'
}

apply plugin: 'java'
apply plugin: 'antlr'

sourceCompatibility = 1.8
group = "beam"
version = "1.0"

defaultTasks 'build'

repositories {
    mavenCentral()
    maven {
        url "https://artifactory.psdops.com/public"
    }

    maven {
        url "https://repo.gradle.org/gradle/libs-releases"
    }
}

dependencies {
    compile group: 'com.psddev', name: 'dari-util', version: '3.2-SNAPSHOT'
    antlr "org.antlr:antlr4:4.7.1"
}

final PARSER_PACKAGE_NAME = 'beam.parser.antlr4'
generateGrammarSource {
    outputs.cacheIf { true }
    arguments += ["-package", PARSER_PACKAGE_NAME, "-Xexact-output-dir"]

    doLast {
        def parserFilePattern = 'Groovy*'
        def outputPath = generateGrammarSource.outputDirectory.canonicalPath
        def parserPackagePath = "${outputPath}/${PARSER_PACKAGE_NAME.replace('.', '/')}"
        file(parserPackagePath).mkdirs()
        copy {
            from outputPath
            into parserPackagePath
            include parserFilePattern
        }
        delete fileTree(outputPath) {
            include parserFilePattern
        }
    }
}

publishing {
    publications {
        beam(MavenPublication) {
            artifactId = 'beam-lang'
            from components.java
        }
    }

    repositories {
        mavenLocal()
    }
}
