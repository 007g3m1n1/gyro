buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
}

plugins {
    id 'maven-publish'
}

defaultTasks 'build', 'checkstyle', 'publish', 'publishToMavenLocal', 'distDir', 'dist'

allprojects {
    version = '0.5'
}

subprojects {
    apply plugin: "idea"
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'checkstyle'

    defaultTasks 'build', 'publish', 'publishToMavenLocal'

    group = 'beam'
    version = '0.5'

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://artifactory.psdops.com/public"
        }

        maven {
            url "https://repo.gradle.org/gradle/libs-releases"
        }
    }

    dependencies {
        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.16'
        compile group: 'org.slf4j', name: 'log4j-over-slf4j', version: '1.7.12'
        compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
        compile group: 'org.lucee', name: 'dom4j', version: '1.6.1'
        checkstyle 'com.puppycrawl.tools:checkstyle:8.15'
    }

    publishing {
        publications {
            beam(MavenPublication) {
                from components.java
                afterEvaluate {
                    artifactId = jar.baseName
                }
            }
        }
    }

    checkstyle {
        toolVersion = '6.13'
        configFile rootProject.file('config/checkstyle.xml')
        checkstyleMain.enabled = false
        checkstyleTest.enabled = false
    }

    task checkstyle(type: Checkstyle) {
        configFile rootProject.file('config/checkstyle.xml')
        source 'src'
        include '**/*.java'
        exclude '**/generated-src/**'

        classpath = files()
    }

}

project(":beam-cli") {
    dependencies {
        compile project(':beam-core')
    }

    publish {
        dependsOn assemble
    }
}

project(":beam-plugins:beam-ssh-plugin") {
    dependencies {
        compile project(':beam-core')
    }
}

project(":beam-plugins:beam-enterprise-plugin") {
    dependencies {
        compile project(':beam-providers:beam-aws-provider')
    }
}

project(":beam-plugins:beam-sample-plugin") {
    dependencies {
        compile project(':beam-core')
        compile project(':beam-providers:beam-aws-provider')
    }
}

project(":beam-providers:beam-aws-provider") {
    dependencies {
        compile project(':beam-core')
    }
}

project(":beam-providers:beam-openstack-provider") {
    dependencies {
        compile project(':beam-core')
    }
}

project(":beam-providers:beam-azure-provider") {
    dependencies {
        compile project(':beam-core')
    }
}

task distDir {
    doLast {
        mkdir "dist"
    }
}
