defaultTasks 'build', 'checkstyle', 'publishToMavenLocal'

allprojects {
    group = 'gyro'
    version = '0.14-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'maven-publish'

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    repositories {
        mavenCentral()
        maven {
            url "https://artifactory.psdops.com/public"
        }
    }

    dependencies {
        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.16'
        compile group: 'org.slf4j', name: 'log4j-over-slf4j', version: '1.7.12'
        compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
        compile group: 'org.lucee', name: 'dom4j', version: '1.6.1'
        checkstyle 'com.puppycrawl.tools:checkstyle:8.15'
    }

    checkstyle {
        toolVersion = '6.13'
        configFile rootProject.file('config/checkstyle.xml')
        checkstyleMain.enabled = false
        checkstyleTest.enabled = false
    }

    task checkstyle(type: Checkstyle) {
        configFile rootProject.file('config/checkstyle.xml')
        source 'src'
        include '**/*.java'
        exclude '**/generated-src/**'

        classpath = files()
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
            }
        }

        repositories {
            maven {
                credentials {
                    username System.getenv("GRADLE_DEPLOY_USER")
                    password System.getenv("GRADLE_DEPLOY_PASSWORD")
                }

                if (project.version.endsWith('-SNAPSHOT')) {
                    url "https://artifactory.psdops.com/gyro-snapshots"

                } else {
                    url "https://artifactory.psdops.com/gyro-releases"
                }
            }
        }
    }
}
